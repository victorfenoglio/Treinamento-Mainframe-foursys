      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. MD022203.                                            00040000
      *===================================================*             00050000
      *  AUTOR   : VICTOR LEAL                            *             00060000
      *  EMPRESA : FOURSYS                                *             00070000
      *  OBJETIVO: UTILIZAR O COMANDO START E STRING.     *             00080006
      *---------------------------------------------------*             00080500
      *  ARQUIVOS:                                        *             00080600
      *  DDNAME             I/O           INCLUDE/BOOK    *             00080700
      *  EVSA0407            I             ---------      *             00080800
      *  SVSA0407            O             ---------      *             00080915
      *===================================================*             00081000
                                                                        00081100
      *====================================================             00081200
       ENVIRONMENT                               DIVISION.              00081300
      *====================================================             00081400
       CONFIGURATION                             SECTION.               00081500
       SPECIAL-NAMES.                                                   00081600
           DECIMAL-POINT  IS COMMA.                                     00081700
                                                                        00081800
       INPUT-OUTPUT                              SECTION.               00081900
       FILE-CONTROL.                                                    00082000
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00083000
               ORGANIZATION IS INDEXED                                  00084000
               ACCESS MODE  IS DYNAMIC                                  00085000
               RECORD KEY   IS ARQ-CHAVE                                00086000
               FILE STATUS  IS WRK-FS-EVSA0407.                         00087000
                                                                        00088000
           SELECT SVSA0407 ASSIGN TO SVSA0407                           00089000
               FILE STATUS IS WRK-FS-SVSA0407.                          00090000
                                                                        00100000
      *====================================================             00110000
       DATA                                      DIVISION.              00120000
      *====================================================             00130000
      *----------------------------------------------------             00130100
       FILE                                      SECTION.               00130200
      *----------------------------------------------------             00130300
       FD EVSA0407.                                                     00130400
       01 REG-EVSA0407.                                                 00130500
          05 ARQ-CHAVE.                                                 00130600
             10 ARQ-AGENCIA     PIC X(04).                              00130700
             10 ARQ-CONTA       PIC X(05).                              00130800
          05 ARQ-SALARIO        PIC X(10).                              00130900
                                                                        00131000
       FD SVSA0407                                                      00132000
            RECORDING MODE IS F                                         00133000
            BLOCK CONTAINS 0 RECORDS.                                   00134000
       01 FD-SVSA0407-REG       PIC X(19).                              00134116
                                                                        00134200
      *----------------------------------------------------             00134300
       WORKING-STORAGE                           SECTION.               00134400
      *----------------------------------------------------             00134502
       01 WRK-EVSA0407.                                                 00134804
          05 WRK-CHAVE.                                                 00134904
             10 WRK-AGENCIA     PIC X(04).                              00135004
             10 WRK-CONTA       PIC X(05).                              00135104
          05 WRK-SALARIO        PIC X(10).                              00135204
       77 WRK-SAIDA-REGISTRO PIC X(19).                                 00135516
      *----------------------------------------------------             00135602
       01 FILLER PIC X(48) VALUE                                        00135702
           '--------------AREA FILE STATUS---------------'.             00135802
      *----------------------------------------------------             00136002
       77 WRK-FS-EVSA0407    PIC 9(02).                                 00136102
       77 WRK-FS-SVSA0407    PIC 9(02).                                 00136203
      *----------------------------------------------------             00136309
       01 FILLER PIC X(48) VALUE                                        00136409
           '--------------AREA PARA ACUMULAR-------------'.             00136509
      *----------------------------------------------------             00136609
       77 WRK-REGLIDOS       PIC 9(02) VALUE ZEROS.                     00136709
       77 WRK-REGGRAV        PIC 9(02) VALUE ZEROS.                     00136809
       77 WRK-REGEXCE        PIC 9(02) VALUE ZEROS.                     00136909
      *----------------------------------------------------             00137102
       LINKAGE                                   SECTION.               00137202
      *----------------------------------------------------             00137302
       01 LNK-ENTRADA.                                                  00137402
          05 LNK-LEN              PIC 9(04) COMP.                       00137502
          05 LNK-CORPO.                                                 00137602
             10 LNK-CHAVE.                                              00137802
                15 LNK-AGENCIA    PIC X(04).                            00137902
                15 LNK-CONTA      PIC X(05).                            00138002
             10 LNK-CHAVEF.                                             00138103
                15 LNK-AGENCIAF   PIC X(04).                            00138203
                15 LNK-CONTAF     PIC X(05).                            00138303
      *====================================================             00138902
       PROCEDURE DIVISION USING LNK-ENTRADA.                            00139002
      *====================================================             00139102
      *----------------------------------------------------             00139200
       0000-PRINCIPAL                             SECTION.              00139300
      *----------------------------------------------------             00139402
           PERFORM 1000-INICIALIZAR.                                    00139500
           PERFORM 2000-PROCESSAR.                                      00139602
           PERFORM 3000-FINALIZAR.                                      00139800
           STOP RUN.                                                    00139900
      *-----------------------------------------------------            00140000
       0000-99-FIM.                                  EXIT.              00140100
      *-----------------------------------------------------            00140200
                                                                        00140300
      *-----------------------------------------------------            00140400
       1000-INICIALIZAR                           SECTION.              00140500
      *-----------------------------------------------------            00140600
           OPEN INPUT EVSA0407.                                         00140700
            PERFORM 1100-TESTA-EVSA0407.                                00140800
                                                                        00140900
           OPEN OUTPUT SVSA0407.                                        00141000
            PERFORM 1200-TESTA-SVSA0407.                                00141100
      *-----------------------------------------------------            00141500
       1000-99-FIM.                                  EXIT.              00141600
      *-----------------------------------------------------            00141700
                                                                        00141800
      *-----------------------------------------------------            00141900
       1100-TESTA-EVSA0407                        SECTION.              00142000
      *-----------------------------------------------------            00142100
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00142200
            DISPLAY 'ERRO NA ABERTURA DO ARQUIVO EVSA0407'              00143000
            DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                     00143100
            GOBACK                                                      00143200
           END-IF.                                                      00143300
      *-----------------------------------------------------            00143400
       1100-99-FIM.                                  EXIT.              00143500
      *-----------------------------------------------------            00143600
                                                                        00143700
      *-----------------------------------------------------            00143800
       1200-TESTA-SVSA0407                        SECTION.              00143900
      *-----------------------------------------------------            00144000
           IF WRK-FS-SVSA0407 NOT EQUAL ZEROS                           00144100
            DISPLAY 'ERRO NA ABERTURA DO ARQUIVO SVSA0407'              00144200
            DISPLAY 'CODIGO VSAM: ' WRK-FS-SVSA0407                     00144300
            GOBACK                                                      00144400
           END-IF.                                                      00144500
      *-----------------------------------------------------            00144600
       1200-99-FIM.                                  EXIT.              00144700
      *-----------------------------------------------------            00144800
                                                                        00144900
      *-----------------------------------------------------            00145000
       2000-PROCESSAR                             SECTION.              00145100
      *-----------------------------------------------------            00145200
           MOVE LNK-CHAVE TO ARQ-CHAVE.                                 00145304
            START EVSA0407 KEY EQUAL ARQ-CHAVE                          00145404
             INVALID KEY                                                00145503
               DISPLAY 'CHAVE NAO ENCONTRADA'                           00145603
             NOT INVALID KEY                                            00145703
               PERFORM 2100-LER-REGISTROS                               00145803
            END-START.                                                  00145903
      *-----------------------------------------------------            00147500
       2000-99-FIM.                                  EXIT.              00147600
      *-----------------------------------------------------            00147700
                                                                        00147800
      *-----------------------------------------------------            00147903
       2100-LER-REGISTROS                         SECTION.              00148003
      *-----------------------------------------------------            00148103
           READ EVSA0407 NEXT.                                          00148203
           PERFORM UNTIL WRK-FS-EVSA0407 EQUAL 10                       00148604
            IF ARQ-CHAVE LESS OR EQUAL LNK-CHAVEF                       00148904
             ADD 1 TO WRK-REGLIDOS                                      00149013
             STRING ARQ-AGENCIA DELIMITED BY SIZE                       00149104
                    ARQ-CONTA   DELIMITED BY SIZE                       00149204
                    ARQ-SALARIO DELIMITED BY SIZE                       00149304
                     INTO WRK-SAIDA-REGISTRO                            00149403
             END-STRING                                                 00149503
              IF ARQ-SALARIO EQUAL TO ZERO OR ARQ-SALARIO IS            00149610
                 NOT NUMERIC                                            00149710
               ADD 1 TO WRK-REGEXCE                                     00149810
               DISPLAY 'SALARIO DESPREZADO: ' WRK-SAIDA-REGISTRO        00149914
              ELSE                                                      00150010
               ADD 1 TO WRK-REGGRAV                                     00150110
               WRITE FD-SVSA0407-REG FROM WRK-SAIDA-REGISTRO            00150210
              END-IF                                                    00150310
             END-IF                                                     00150410
           READ EVSA0407 NEXT                                           00150504
           END-PERFORM.                                                 00150603
      *-----------------------------------------------------            00150703
       2100-99-FIM.                                  EXIT.              00150803
      *-----------------------------------------------------            00150903
                                                                        00151003
      *-----------------------------------------------------            00151100
       3000-FINALIZAR                             SECTION.              00151200
      *-----------------------------------------------------            00151300
           CLOSE EVSA0407.                                              00151400
            PERFORM 3100-TESTA-EVSA0407.                                00152003
           CLOSE SVSA0407.                                              00160000
            PERFORM 3200-TESTA-SVSA0407.                                00160103
                                                                        00160209
           DISPLAY '  '.                                                00160309
           DISPLAY 'REGISTROS LIDOS: '    WRK-REGLIDOS.                 00160409
           DISPLAY 'REGISTROS GRAVADOS: ' WRK-REGGRAV.                  00160509
           DISPLAY 'REGISTROS EXCECAO: '  WRK-REGEXCE.                  00160609
           DISPLAY 'FIM DE PROCESSAMENTO'.                              00160700
      *-----------------------------------------------------            00160800
       3000-99-FIM.                                  EXIT.              00160900
      *-----------------------------------------------------            00161000
                                                                        00161103
      *-----------------------------------------------------            00161203
       3100-TESTA-EVSA0407                        SECTION.              00161303
      *-----------------------------------------------------            00161403
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00161503
            DISPLAY 'ERRO NO FECHAMENTO DO ARQUIVO EVSA0407'            00161603
            DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                     00161703
            GOBACK                                                      00161803
           END-IF.                                                      00161903
      *-----------------------------------------------------            00162003
       3100-99-FIM.                                  EXIT.              00162103
      *-----------------------------------------------------            00162203
                                                                        00162303
      *-----------------------------------------------------            00162403
       3200-TESTA-SVSA0407                        SECTION.              00162503
      *-----------------------------------------------------            00162603
           IF WRK-FS-SVSA0407 NOT EQUAL ZEROS                           00162703
            DISPLAY 'ERRO NO FECHAMENTO DO ARQUIVO SVSA0407'            00162803
            DISPLAY 'CODIGO VSAM: ' WRK-FS-SVSA0407                     00162903
            GOBACK                                                      00163003
           END-IF.                                                      00163103
      *-----------------------------------------------------            00163203
       3200-99-FIM.                                  EXIT.              00163303
      *-----------------------------------------------------            00163403
