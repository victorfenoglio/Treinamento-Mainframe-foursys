      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR22DB01.                                            00040000
      *===================================================*             00050006
      *  AUTOR   : VICTOR LEAL                            *             00060006
      *  EMPRESA : FOURSYS                                *             00070006
      *  OBJETIVO: LEITURA DE UM REGISTRO USANDO DB2.     *             00080006
      *---------------------------------------------------*             00080106
      *  BASE  DE DADOS:                                  *             00080206
      *  TABELA             I/O           INCLUDE/BOOK    *             00080306
      *  IVAN.FUNC           I               #BKFUNC      *             00080406
      *===================================================*             00081006
                                                                        00082000
      *====================================================             00083000
       ENVIRONMENT                               DIVISION.              00084000
      *====================================================             00085000
       CONFIGURATION                             SECTION.               00086000
       SPECIAL-NAMES.                                                   00087000
           DECIMAL-POINT  IS COMMA.                                     00088000
                                                                        00089000
      *====================================================             00130400
       DATA                                      DIVISION.              00130500
      *====================================================             00130600
      *-----------------------------------------------------            00130700
       FILE                                      SECTION.               00130800
      *-----------------------------------------------------            00130900
                                                                        00137000
      *-----------------------------------------------------            00137100
       WORKING-STORAGE                           SECTION.               00137200
      *-----------------------------------------------------            00137300
           EXEC SQL                                                     00137500
              INCLUDE #BKFUNC                                           00137600
           END-EXEC.                                                    00137700
                                                                        00137800
           EXEC SQL                                                     00137900
              INCLUDE SQLCA                                             00138000
           END-EXEC.                                                    00138100
      *----------------------------------------------------             00138206
       01 FILLER PIC X(48) VALUE                                        00138306
           '--------------VARIAVEIS PARA APOIO-----------'.             00138406
      *----------------------------------------------------             00138506
       77 WRK-ID          PIC 9(05) VALUE ZEROS.                        00138600
       77 WRK-SQLCODE     PIC -999.                                     00138700
       77 WRK-NULL-EMAIL  PIC S9(04) COMP.                              00138800
      *====================================================             00138900
       PROCEDURE                                 DIVISION.              00139000
      *====================================================             00139100
      *-----------------------------------------------------            00139200
       0000-PRINCIPAL                             SECTION.              00139300
      *-----------------------------------------------------            00139400
           PERFORM 1000-INICIALIZAR.                                    00139500
           PERFORM 2000-PROCESSAR.                                      00139600
           PERFORM 3000-FINALIZAR.                                      00139700
           GOBACK.                                                      00139800
      *-----------------------------------------------------            00139900
       0000-99-FIM.                                  EXIT.              00140000
      *-----------------------------------------------------            00140100
                                                                        00140200
      *-----------------------------------------------------            00140300
       1000-INICIALIZAR                           SECTION.              00140400
      *-----------------------------------------------------            00140500
           ACCEPT WRK-ID FROM SYSIN.                                    00140600
      *-----------------------------------------------------            00144000
       1000-99-FIM.                                  EXIT.              00145000
      *-----------------------------------------------------            00146000
                                                                        00147000
      *-----------------------------------------------------            00148000
       2000-PROCESSAR                             SECTION.              00149000
      *-----------------------------------------------------            00150000
           MOVE WRK-ID TO DB2-ID.                                       00151000
                                                                        00152000
           EXEC SQL                                                     00153000
            SELECT ID,NOME,SETOR,SALARIO,DATAADM,EMAIL                  00154000
            INTO :DB2-ID,                                               00155000
                 :DB2-NOME,                                             00156000
                 :DB2-SETOR,                                            00157000
                 :DB2-SALARIO,                                          00157100
                 :DB2-DATAADM,                                          00157200
                 :DB2-EMAIL      :WRK-NULL-EMAIL                        00157300
           FROM IVAN.FUNC                                               00157401
            WHERE ID = :DB2-ID                                          00157501
           END-EXEC.                                                    00157600
                                                                        00157700
           EVALUATE SQLCODE                                             00157800
            WHEN 0                                                      00157900
             DISPLAY 'ID: '      DB2-ID                                 00158004
             DISPLAY 'NOME:'     DB2-NOME                               00158104
             DISPLAY 'SETOR:'    DB2-SETOR                              00158204
             DISPLAY 'SALARIO:'  DB2-SALARIO                            00158304
             DISPLAY 'DATAADM:'  DB2-DATAADM                            00158404
              IF WRK-NULL-EMAIL = 0                                     00158500
               DISPLAY 'EMAIL' DB2-EMAIL                                00158600
              ELSE                                                      00158700
               DISPLAY 'EMAIL NULO'                                     00158800
              END-IF                                                    00158900
                                                                        00159000
            WHEN 100                                                    00159105
             DISPLAY 'REGISTRO NAO ENCONTRADO' DB2-ID                   00159205
                                                                        00159300
            WHEN OTHER                                                  00159405
             MOVE SQLCODE TO WRK-SQLCODE                                00159505
             DISPLAY 'ERRO NA LEITURA' WRK-SQLCODE                      00159605
           END-EVALUATE.                                                00159700
      *-----------------------------------------------------            00159800
       2000-99-FIM.                                  EXIT.              00159900
      *-----------------------------------------------------            00160000
                                                                        00160100
      *-----------------------------------------------------            00160200
       3000-FINALIZAR                             SECTION.              00160300
      *-----------------------------------------------------            00160400
           DISPLAY 'FIM DO PROGRAMA.'.                                  00160502
      *-----------------------------------------------------            00160600
       3000-99-FIM.                                  EXIT.              00160700
      *-----------------------------------------------------            00160800
