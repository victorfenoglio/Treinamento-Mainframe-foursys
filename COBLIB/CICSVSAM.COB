      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00011000
      *====================================================             00012000
       PROGRAM-ID. CICSVSAM.                                            00013019
      *===================================================*             00014000
      *  AUTOR   : VICTOR LEAL                            *             00015004
      *  EMPRESA : FOURSYS                                *             00016004
      *  OBJETIVO: PROGRAMA DE LEITURA MAPA BMS CICS COM  *             00017000
      *            ACESSO AOS REGISTROS EM ARQUIVO VSAM,  *             00018017
      *            UTILIZANDO LOGICA PSEUDO DE SAIDA.     *             00018117
      *---------------------------------------------------*             00019000
      *  ARQUIVOS:                                        *             00020000
      *  DDNAME             I/O           INCLUDE/BOOK    *             00021000
      *  F22CIM1             I             ---------      *             00022018
      *---------------------------------------------------*             00023000
      *  BASE  DE DADOS:                                  *             00024000
      *  TABELA             I/O           INCLUDE/BOOK    *             00025000
      *  DFH310.CICS.FUNC    I             ---------      *             00026005
      *===================================================*             00027000
       DATA                                      DIVISION.              00028000
      *====================================================             00029000
      *-----------------------------------------------------            00030000
       WORKING-STORAGE                            SECTION.              00040000
      *-----------------------------------------------------            00041000
            COPY F22CIM1.                                               00042000
             COPY DFHAID.                                               00043000
                                                                        00052000
       01 WRK-DADOS.                                                    00053000
          05 WRK-CHAVE       PIC X(05).                                 00054001
          05 WRK-NOME        PIC X(30).                                 00055001
          05 WRK-SETOR       PIC X(04).                                 00056001
          05 WRK-SALARIO     PIC X(10).                                 00057001
          05 WRK-DATAADM.                                               00058001
             10 WRK-DATADIA  PIC X(02).                                 00058103
             10 FILLER       PIC X(01).                                 00058201
             10 WRK-DATAMES  PIC X(02).                                 00058301
             10 FILLER       PIC X(01).                                 00058401
             10 WRK-DATAANO  PIC X(04).                                 00058503
          05 WRK-EMAIL       PIC X(40).                                 00058601
                                                                        00059000
      *----------------------------------------------------             00059100
       01 FILLER PIC X(48) VALUE                                        00059200
           '--------------VARIAVEIS DE APOIO-------------'.             00059307
      *----------------------------------------------------             00059400
       77 WRK-RESP       PIC S9(04) COMP.                               00059507
       77 WRK-RET-MAPA   PIC S9(04) COMP.                               00059607
       77 WRK-FLAG       PIC  9(1).                                     00059711
       77 WRK-SAIDA      PIC  X(14) VALUE '---OBRIGADO---'.             00059807
      *====================================================             00059900
       PROCEDURE                                 DIVISION.              00060000
      *====================================================             00061000
      *-----------------------------------------------------            00070000
       0000-PRINCIPAL                             SECTION.              00071000
      *-----------------------------------------------------            00072000
            PERFORM 1000-INICIALIZAR.                                   00073000
             IF WRK-FLAG EQUAL 1                                        00073107
              PERFORM 2000-PROCESSAR                                    00073207
             END-IF.                                                    00073307
            PERFORM 3000-FINALIZAR.                                     00075000
                                                                        00075107
            EXEC CICS                                                   00075207
             RETURN TRANSID ('T222')                                    00075307
            END-EXEC.                                                   00075407
      *-----------------------------------------------------            00076000
       0000-99-FIM.                                  EXIT.              00077000
      *-----------------------------------------------------            00078000
                                                                        00079000
      *-----------------------------------------------------            00079100
       1000-INICIALIZAR                           SECTION.              00079200
      *-----------------------------------------------------            00079300
             EXEC CICS RECEIVE                                          00079407
               MAPSET('F22CIM1')                                        00079500
               MAP('MAPA01')                                            00079600
               INTO(MAPA01I)                                            00079707
               RESP(WRK-RET-MAPA)                                       00079807
             END-EXEC.                                                  00079907
                                                                        00080007
           IF WRK-RET-MAPA EQUAL DFHRESP(MAPFAIL)                       00080107
            MOVE 2 TO WRK-FLAG                                          00080207
           ELSE                                                         00080307
            MOVE 1 TO WRK-FLAG                                          00080407
           END-IF.                                                      00080507
      *-----------------------------------------------------            00081000
       1000-99-FIM.                                  EXIT.              00090000
      *-----------------------------------------------------            00100000
                                                                        00110000
      *-----------------------------------------------------            00120000
       2000-PROCESSAR                             SECTION.              00130000
      *-----------------------------------------------------            00140000
           EVALUATE TRUE                                                00192007
             WHEN EIBAID = ''''                                         00193015
              PERFORM 2050-LIMPAR                                       00193113
             WHEN EIBAID = ':'                                          00195008
              PERFORM 2100-SAIR                                         00196007
             WHEN EIBAID = '5'                                          00197000
              PERFORM 2150-CONSULTAR                                    00198000
            END-EVALUATE.                                               00199415
      *-----------------------------------------------------            00200200
       2000-99-FIM.                                  EXIT.              00200300
      *-----------------------------------------------------            00200400
                                                                        00200513
      *-----------------------------------------------------            00200613
       2050-LIMPAR                                SECTION.              00200713
      *-----------------------------------------------------            00200813
           INITIALIZE MAPA01O.                                          00200914
      *-----------------------------------------------------            00201313
       2050-99-FIM.                                  EXIT.              00201413
      *-----------------------------------------------------            00201513
                                                                        00201607
      *-----------------------------------------------------            00201707
       2100-SAIR                                  SECTION.              00201807
      *-----------------------------------------------------            00201907
            EXEC CICS SEND                                              00202007
             FROM(WRK-SAIDA)                                            00202107
             ERASE                                                      00202207
            END-EXEC.                                                   00202307
                                                                        00202407
            EXEC CICS                                                   00202507
             RETURN                                                     00202607
            END-EXEC.                                                   00202707
      *-----------------------------------------------------            00202807
       2100-99-FIM.                                  EXIT.              00202907
      *-----------------------------------------------------            00203007
                                                                        00203100
      *-----------------------------------------------------            00203200
       2150-CONSULTAR                             SECTION.              00203300
      *-----------------------------------------------------            00203400
             MOVE IDI TO WRK-CHAVE.                                     00203500
              EXEC CICS READ                                            00203600
                        FILE('FUNC')                                    00204000
                        RIDFLD(WRK-CHAVE)                               00205000
                        INTO(WRK-DADOS)                                 00206000
                        RESP(WRK-RESP)                                  00207000
              END-EXEC.                                                 00214000
                                                                        00215000
             IF WRK-RESP EQUAL DFHRESP(NORMAL)                          00216001
              MOVE WRK-NOME                  TO NOMEO                   00218000
              MOVE WRK-SETOR                 TO SETORO                  00219000
              MOVE WRK-SALARIO               TO SALARIOO                00220000
              MOVE WRK-DATADIA               TO DATADIAO                00240000
              MOVE WRK-DATAMES               TO DATAMESO                00250000
              MOVE WRK-DATAANO               TO DATAANOO                00251000
              MOVE WRK-EMAIL                 TO EMAILO                  00253000
               IF WRK-SALARIO EQUAL ZEROS                               00253102
                MOVE 'ENCONTRADO-INCONSISTENCIA SALARIO'                00253202
                      TO MSGO                                           00253302
               ELSE                                                     00253402
                MOVE 'REGISTRO ENCONTRADO'   TO MSGO                    00253502
               END-IF                                                   00253602
             END-IF.                                                    00253702
                                                                        00253802
             IF WRK-RESP EQUAL DFHRESP(NOTFND)                          00254002
              MOVE 'REGISTRO NAO ENCONTRADO' TO MSGO                    00255000
             END-IF.                                                    00280000
      *-----------------------------------------------------            00290000
       2150-99-FIM.                                  EXIT.              00291000
      *-----------------------------------------------------            00292000
                                                                        00292100
      *-----------------------------------------------------            00316000
       3000-FINALIZAR                             SECTION.              00317000
      *-----------------------------------------------------            00318000
           IF WRK-FLAG EQUAL 2                                          00319007
             EXEC CICS SEND                                             00320007
               MAPSET('F22CIM1')                                        00330007
               MAP('MAPA01')                                            00331007
                ERASE                                                   00332007
               MAPONLY                                                  00333007
             END-EXEC                                                   00334007
           ELSE                                                         00335007
             EXEC CICS SEND                                             00336007
               MAPSET('F22CIM1')                                        00337007
               MAP('MAPA01')                                            00338007
               DATAONLY                                                 00339007
               FROM(MAPA01O)                                            00339107
             END-EXEC                                                   00339207
           END-IF.                                                      00339307
      *-----------------------------------------------------            00340000
       3000-99-FIM.                                  EXIT.              00350000
      *-----------------------------------------------------            00360000
