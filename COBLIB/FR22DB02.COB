      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. FR22DB02.                                            00040000
      *===================================================*             00050012
      *  AUTOR   : VICTOR LEAL                            *             00060015
      *  EMPRESA : FOURSYS                                *             00070015
      *  OBJETIVO: LEITURA DE TODOS OS REGISTROS DE UMA   *             00080012
      *            TABELA USANDO DB2.                     *             00080113
      *---------------------------------------------------*             00080212
      *  BASE  DE DADOS:                                  *             00080313
      *  TABELA             I/O           INCLUDE/BOOK    *             00080413
      *  IVAN.FUNC           I               #BKFUNC      *             00080512
      *===================================================*             00081012
                                                                        00082000
      *====================================================             00083000
       ENVIRONMENT                               DIVISION.              00084000
      *====================================================             00085000
       CONFIGURATION                             SECTION.               00086000
       SPECIAL-NAMES.                                                   00087000
           DECIMAL-POINT  IS COMMA.                                     00088000
                                                                        00089000
      *====================================================             00090000
       DATA                                      DIVISION.              00100000
      *====================================================             00110000
      *----------------------------------------------------             00120012
       FILE                                      SECTION.               00130000
      *----------------------------------------------------             00131012
                                                                        00132000
      *-----------------------------------------------------            00133000
       WORKING-STORAGE                           SECTION.               00134000
      *-----------------------------------------------------            00135000
           EXEC SQL                                                     00136000
              INCLUDE #BKFUNC                                           00137000
           END-EXEC.                                                    00137100
                                                                        00137200
           EXEC SQL                                                     00137300
              INCLUDE SQLCA                                             00137400
           END-EXEC.                                                    00137500
                                                                        00137600
           EXEC SQL                                                     00137700
              DECLARE CFUNC CURSOR FOR                                  00137800
               SELECT * FROM IVAN.FUNC                                  00137900
                ORDER BY ID                                             00138003
           END-EXEC.                                                    00138100
                                                                        00138214
      *----------------------------------------------------             00138314
       01 FILLER PIC X(48) VALUE                                        00138414
           '--------------VARIAVEIS PARA APOIO-----------'.             00138514
      *----------------------------------------------------             00138614
       77 WRK-ID          PIC 9(05)    VALUE ZEROS.                     00138708
       77 WRK-SQLCODE     PIC -999.                                     00138800
       77 WRK-NULL-EMAIL  PIC S9(04) COMP.                              00138900
      *----------------------------------------------------             00139014
       01 FILLER PIC X(48) VALUE                                        00139114
           '--------------VARIAVEIS CALCULAR/ACUMULAR----'.             00139214
      *----------------------------------------------------             00139314
       77 WRK-MEDIA       PIC 9(08)V99 VALUE ZEROS.                     00139409
       77 WRK-REGLIDOS    PIC 9(02)    VALUE ZEROS.                     00139508
       77 WRK-SALACUM     PIC 9(08)V99 VALUE ZEROS.                     00139608
      *====================================================             00139700
       PROCEDURE                                 DIVISION.              00139800
      *====================================================             00139900
      *-----------------------------------------------------            00140000
       0000-PRINCIPAL                             SECTION.              00140100
      *-----------------------------------------------------            00140200
           PERFORM 1000-INICIALIZAR.                                    00140300
           PERFORM 2000-PROCESSAR UNTIL SQLCODE EQUAL 100.              00140400
           PERFORM 3000-FINALIZAR.                                      00140500
           STOP RUN.                                                    00140600
      *-----------------------------------------------------            00140700
       0000-99-FIM.                                  EXIT.              00140800
      *-----------------------------------------------------            00140900
                                                                        00141000
      *-----------------------------------------------------            00141100
       1000-INICIALIZAR                           SECTION.              00141200
      *-----------------------------------------------------            00141300
           EXEC SQL                                                     00141400
              OPEN CFUNC                                                00141501
           END-EXEC.                                                    00141600
                                                                        00141700
           EVALUATE SQLCODE                                             00141800
            WHEN 0                                                      00141900
             PERFORM 4000-LER-FUNCIONARIO                               00142002
            WHEN 100                                                    00142100
             DISPLAY 'SEM FUNCIONARIOS'                                 00142200
            WHEN OTHER                                                  00142300
             MOVE SQLCODE TO WRK-SQLCODE                                00142400
             DISPLAY 'ERRO' WRK-SQLCODE 'NO OPEN CURSOR'                00142502
             STOP RUN                                                   00142600
           END-EVALUATE.                                                00142711
      *-----------------------------------------------------            00142800
       1000-99-FIM.                                  EXIT.              00142900
      *-----------------------------------------------------            00143000
                                                                        00143100
      *-----------------------------------------------------            00143200
       2000-PROCESSAR                             SECTION.              00143300
      *-----------------------------------------------------            00144000
           DISPLAY '---------------------------------------'            00144103
           DISPLAY 'ID:     ' DB2-ID                                    00144200
           DISPLAY 'NOME:   ' DB2-NOME                                  00144300
           DISPLAY 'SETOR:  ' DB2-SETOR                                 00144400
           DISPLAY 'SALARIO:' DB2-SALARIO                               00144500
           DISPLAY 'DATAADM:' DB2-DATAADM                               00144600
            IF WRK-NULL-EMAIL = 0                                       00144700
             DISPLAY 'EMAIL' DB2-EMAIL                                  00144800
            ELSE                                                        00144900
             DISPLAY 'EMAIL NULO'                                       00145000
            END-IF.                                                     00145100
                                                                        00145200
           PERFORM 4000-LER-FUNCIONARIO.                                00145300
                                                                        00145400
      *-----------------------------------------------------            00158900
       2000-99-FIM.                                  EXIT.              00159000
      *-----------------------------------------------------            00160000
                                                                        00160100
      *-----------------------------------------------------            00160200
       3000-FINALIZAR                             SECTION.              00160300
      *-----------------------------------------------------            00160400
           EXEC SQL                                                     00160500
              CLOSE CFUNC                                               00160600
           END-EXEC.                                                    00160700
      *-----------------------------------------------------            00160800
       3000-99-FIM.                                  EXIT.              00160900
      *-----------------------------------------------------            00161000
                                                                        00162000
      *-----------------------------------------------------            00163000
       4000-LER-FUNCIONARIO                       SECTION.              00164000
      *-----------------------------------------------------            00165000
           EXEC SQL                                                     00166000
             FETCH CFUNC                                                00166100
              INTO :DB2-ID,                                             00166200
                   :DB2-NOME,                                           00166300
                   :DB2-SETOR,                                          00166400
                   :DB2-SALARIO,                                        00166500
                   :DB2-DATAADM,                                        00166600
                   :DB2-EMAIL      :WRK-NULL-EMAIL                      00166700
           END-EXEC.                                                    00166800
                                                                        00166900
           EVALUATE SQLCODE                                             00167000
            WHEN 0                                                      00167100
             CONTINUE                                                   00167200
             ADD 1 TO WRK-REGLIDOS                                      00167304
             ADD DB2-SALARIO TO WRK-SALACUM                             00167405
            WHEN 100                                                    00167500
             DISPLAY 'FINAL DA TABELA'                                  00167604
             DISPLAY ' '                                                00167705
             DISPLAY 'REGISTROS LIDOS: '   WRK-REGLIDOS                 00167809
             DISPLAY 'SOMA DOS SALARIOS:'  WRK-SALACUM                  00167909
              DIVIDE WRK-SALACUM BY WRK-REGLIDOS GIVING                 00168009
                     WRK-MEDIA                                          00168109
             DISPLAY 'MEDIA DOS SALARIOS:' WRK-MEDIA                    00168209
            WHEN OTHER                                                  00168300
             MOVE SQLCODE TO WRK-SQLCODE                                00168400
             DISPLAY 'ERRO' WRK-SQLCODE                                 00168500
           END-EVALUATE.                                                00168600
      *-----------------------------------------------------            00168700
       4000-99-FIM.                                  EXIT.              00168800
      *-----------------------------------------------------            00169000
