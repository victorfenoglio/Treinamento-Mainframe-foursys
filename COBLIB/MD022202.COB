      *====================================================             00010000
       IDENTIFICATION                            DIVISION.              00020000
      *====================================================             00030000
       PROGRAM-ID. MD022202.                                            00040000
      *===================================================*             00050000
      *  AUTOR   : VICTOR LEAL                            *             00060000
      *  EMPRESA : FOURSYS                                *             00070000
      *  OBJETIVO: GRAVAR,LER,REGRAVAR E DELETAR REGISTRO *             00080004
      *            DE ARQUIVOS VSAM, USANDO PARM NO JCL   *             00080104
      *---------------------------------------------------*             00080500
      *  ARQUIVOS:                                        *             00080600
      *  DDNAME             I/O           INCLUDE/BOOK    *             00080700
      *  EVSA0407           I/O            ---------      *             00080802
      *===================================================*             00081000
                                                                        00081100
      *====================================================             00081200
       ENVIRONMENT                               DIVISION.              00081300
      *====================================================             00081400
       CONFIGURATION                             SECTION.               00081500
       SPECIAL-NAMES.                                                   00081600
           DECIMAL-POINT  IS COMMA.                                     00081700
                                                                        00081800
       INPUT-OUTPUT                              SECTION.               00081900
       FILE-CONTROL.                                                    00082000
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00083000
               ORGANIZATION IS INDEXED                                  00084000
               ACCESS MODE  IS DYNAMIC                                  00085001
               RECORD KEY   IS ARQ-CHAVE                                00086000
               FILE STATUS  IS WRK-FS-EVSA0407.                         00087000
                                                                        00088000
      *====================================================             00110000
       DATA                                      DIVISION.              00120000
      *====================================================             00130000
      *----------------------------------------------------             00130100
       FILE                                      SECTION.               00130200
      *----------------------------------------------------             00130300
       FD EVSA0407.                                                     00130400
       01 REG-EVSA0407.                                                 00130500
          05 ARQ-CHAVE.                                                 00130600
             10 ARQ-AGENCIA     PIC X(04).                              00130700
             10 ARQ-CONTA       PIC X(05).                              00130800
          05 ARQ-SALARIO        PIC X(10).                              00130900
                                                                        00134200
      *----------------------------------------------------             00134300
       WORKING-STORAGE                           SECTION.               00134400
      *----------------------------------------------------             00134500
      *----------------------------------------------------             00134700
       01 FILLER PIC X(48) VALUE                                        00134800
           '--------------AREA FILE STATUS---------------'.             00134900
      *----------------------------------------------------             00135000
       77 WRK-LEN            PIC 9999.                                  00135101
       77 WRK-FS-EVSA0407    PIC 9(02).                                 00136000
                                                                        00137001
      *----------------------------------------------------             00137101
       LINKAGE                                   SECTION.               00137201
      *----------------------------------------------------             00137301
       01 LNK-ENTRADA.                                                  00137401
          05 LNK-LEN              PIC 9(04) COMP.                       00137510
          05 LNK-CORPO.                                                 00137601
            10 LNK-OPER           PIC X(01).                            00137710
            10 LNK-INFO.                                                00137810
               15 LNK-CHAVE.                                            00137910
                  20 LNK-AGENCIA  PIC X(04).                            00138010
                  20 LNK-CONTA    PIC X(05).                            00138110
               15 LNK-SAL         PIC X(10).                            00138210
      *====================================================             00138500
       PROCEDURE DIVISION USING LNK-ENTRADA.                            00138601
      *====================================================             00138700
      *----------------------------------------------------             00138800
       0000-PRINCIPAL                             SECTION.              00138900
      *-----------------------------------------------------            00139000
           PERFORM 1000-INICIALIZAR.                                    00139100
           PERFORM 2000-PROCESSAR.                                      00139215
           PERFORM 3000-FINALIZAR.                                      00140315
           GOBACK.                                                      00140401
      *-----------------------------------------------------            00140500
       0000-99-FIM.                                  EXIT.              00140600
      *-----------------------------------------------------            00140700
                                                                        00140800
      *-----------------------------------------------------            00140900
       1000-INICIALIZAR                           SECTION.              00141000
      *-----------------------------------------------------            00141100
           OPEN I-O EVSA0407.                                           00141202
            IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                          00141301
             DISPLAY 'ERRO NO OPEN ARQUIVO EVSA0407'                    00141401
             GOBACK                                                     00141501
            END-IF.                                                     00141601
      *-----------------------------------------------------            00141700
       1000-99-FIM.                                  EXIT.              00141800
      *-----------------------------------------------------            00141900
                                                                        00142015
      *-----------------------------------------------------            00142115
       2000-PROCESSAR                             SECTION.              00142215
      *-----------------------------------------------------            00142315
           EVALUATE LNK-OPER                                            00142415
             WHEN 'L'                                                   00142515
               PERFORM 2100-LEITURA                                     00142615
             WHEN 'G'                                                   00142715
               PERFORM 2200-GRAVACAO                                    00142815
             WHEN 'R'                                                   00142915
               PERFORM 2300-REGRAVAR                                    00143015
             WHEN 'D'                                                   00143115
               PERFORM 2400-DELETAR                                     00143215
           END-EVALUATE.                                                00143315
      *-----------------------------------------------------            00143615
       2000-99-FIM.                                  EXIT.              00143715
      *-----------------------------------------------------            00143815
                                                                        00144300
      *-----------------------------------------------------            00145000
       2100-LEITURA                               SECTION.              00145115
      *-----------------------------------------------------            00145200
           MOVE LNK-AGENCIA TO ARQ-AGENCIA.                             00145301
           MOVE LNK-CONTA   TO ARQ-CONTA.                               00145401
           READ EVSA0407                                                00145514
            IF WRK-FS-EVSA0407 EQUAL ZEROS                              00145601
             DISPLAY 'REGISTRO ENCONTRADO'                              00145704
             DISPLAY REG-EVSA0407                                       00145801
            ELSE                                                        00145901
             DISPLAY 'REGISTRO NAO ENCONTRADO'                          00146001
            END-IF.                                                     00147001
      *-----------------------------------------------------            00147500
       2100-99-FIM.                                  EXIT.              00147615
      *-----------------------------------------------------            00147700
                                                                        00147800
      *-----------------------------------------------------            00147900
       2200-GRAVACAO                              SECTION.              00148015
      *-----------------------------------------------------            00149000
           MOVE LNK-AGENCIA TO ARQ-AGENCIA.                             00150001
           MOVE LNK-CONTA   TO ARQ-CONTA.                               00160001
           MOVE LNK-SAL     TO ARQ-SALARIO.                             00160101
            WRITE REG-EVSA0407                                          00160201
             IF WRK-FS-EVSA0407 EQUAL ZEROS                             00160301
              DISPLAY 'REGISTRO GRAVADO'                                00160404
              DISPLAY REG-EVSA0407                                      00160501
             ELSE                                                       00160601
              DISPLAY 'REGISTRO NAO GRAVADO'                            00160704
             END-IF.                                                    00160801
      *-----------------------------------------------------            00160900
       2200-99-FIM.                                  EXIT.              00161015
      *-----------------------------------------------------            00161100
                                                                        00161203
      *-----------------------------------------------------            00161303
       2300-REGRAVAR                              SECTION.              00161415
      *-----------------------------------------------------            00161503
           REWRITE REG-EVSA0407 FROM LNK-INFO.                          00161612
           IF WRK-FS-EVSA0407 EQUAL ZEROS                               00161710
            DISPLAY 'REGISTRO REGRAVADO'                                00161810
           ELSE                                                         00161910
            DISPLAY 'REGISTRO NAO REGRAVADO'                            00162010
           END-IF.                                                      00162110
      *-----------------------------------------------------            00162203
       2300-99-FIM.                                  EXIT.              00162315
      *-----------------------------------------------------            00162403
                                                                        00162503
      *-----------------------------------------------------            00162603
       2400-DELETAR                               SECTION.              00162715
      *-----------------------------------------------------            00162803
           MOVE LNK-CHAVE TO ARQ-CHAVE.                                 00162904
           DELETE EVSA0407.                                             00163009
           IF WRK-FS-EVSA0407 EQUAL ZEROS                               00163108
            DISPLAY 'REGISTRO DELETADO'                                 00163204
           ELSE                                                         00163304
            DISPLAY 'REGISTRO NAO DELETADO'                             00163404
           END-IF.                                                      00163504
      *-----------------------------------------------------            00163603
       2400-99-FIM.                                  EXIT.              00163715
      *-----------------------------------------------------            00163803
                                                                        00163903
      *-----------------------------------------------------            00164001
       3000-FINALIZAR                             SECTION.              00164115
      *-----------------------------------------------------            00164201
           CLOSE EVSA0407.                                              00164301
      *-----------------------------------------------------            00164401
       3000-99-FIM.                                  EXIT.              00164515
      *-----------------------------------------------------            00165001
