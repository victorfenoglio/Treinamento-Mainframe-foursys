      *====================================================             00010002
       IDENTIFICATION                            DIVISION.              00011002
      *====================================================             00012002
       PROGRAM-ID. F22CIP3.                                             00013004
      *===================================================*             00014002
      *  AUTOR   : VICTOR LEAL                            *             00015002
      *  EMPRESA : FOURSYS                                *             00016002
      *  OBJETIVO: PROGRAMA FUNCIONAL. MAPA BMS CICS COM  *             00017006
      *            ACESSO AOS REGISTROS EM ARQUIVO VSAM,  *             00018002
      *            UTILIZANDO LOGICA PSEUDO DE SAIDA,     *             00018106
      *            COMUNICANDO COM O PROGRAMA F22COVSA.   *             00018206
      *---------------------------------------------------*             00018702
      *  BASE  DE DADOS:                                  *             00018802
      *  TABELA             I/O           INCLUDE/BOOK    *             00018902
      *  DFH310.CICS.FUNC    I             ---------      *             00019002
      *===================================================*             00019102
       DATA                                      DIVISION.              00020002
      *====================================================             00021002
      *-----------------------------------------------------            00022002
       WORKING-STORAGE                            SECTION.              00023002
      *-----------------------------------------------------            00024002
            COPY F22CIM1.                                               00025002
             COPY DFHAID.                                               00026002
                                                                        00027002
       01 WRK-DADOS.                                                    00028002
          05 WRK-CHAVE       PIC X(05).                                 00029002
          05 WRK-NOME        PIC X(30).                                 00030002
          05 WRK-SETOR       PIC X(04).                                 00040002
          05 WRK-SALARIO     PIC 9(08)V99.                              00050008
          05 WRK-DATAADM.                                               00051003
             10 WRK-DATADIA  PIC X(02).                                 00052003
             10 FILLER       PIC X(01).                                 00053003
             10 WRK-DATAMES  PIC X(02).                                 00054003
             10 FILLER       PIC X(01).                                 00055003
             10 WRK-DATAANO  PIC X(04).                                 00056003
          05 WRK-EMAIL       PIC X(40).                                 00057002
          05 WRK-STATUS      PIC X(01).                                 00058002
      *----------------------------------------------------             00059002
       01 FILLER PIC X(48) VALUE                                        00059102
           '--------------VARIAVEIS DE APOIO-------------'.             00059202
      *----------------------------------------------------             00059302
       77 WRK-RESP       PIC S9(04) COMP.                               00059402
       77 WRK-RET-MAPA   PIC S9(04) COMP.                               00059502
       77 WRK-FLAG       PIC  9(1).                                     00059602
       77 WRK-SAIDA      PIC  X(14) VALUE '---OBRIGADO---'.             00059702
      *====================================================             00059802
       PROCEDURE                                 DIVISION.              00059902
      *====================================================             00060002
      *-----------------------------------------------------            00070002
       0000-PRINCIPAL                             SECTION.              00071002
      *-----------------------------------------------------            00072002
            PERFORM 1000-INICIALIZAR.                                   00073002
             IF WRK-FLAG EQUAL 1                                        00073102
              PERFORM 2000-PROCESSAR                                    00073202
             END-IF.                                                    00073302
            PERFORM 3000-FINALIZAR.                                     00073402
                                                                        00073502
            EXEC CICS                                                   00073602
             RETURN TRANSID ('T223')                                    00073702
            END-EXEC.                                                   00073802
      *-----------------------------------------------------            00073902
       0000-99-FIM.                                  EXIT.              00074002
      *-----------------------------------------------------            00075002
                                                                        00076002
      *-----------------------------------------------------            00077002
       1000-INICIALIZAR                           SECTION.              00078002
      *-----------------------------------------------------            00079002
             EXEC CICS RECEIVE                                          00079102
               MAPSET('F22CIM1')                                        00079202
               MAP('MAPA01')                                            00079302
               INTO(MAPA01I)                                            00079402
               RESP(WRK-RET-MAPA)                                       00079502
             END-EXEC.                                                  00079602
                                                                        00079702
           IF WRK-RET-MAPA EQUAL DFHRESP(MAPFAIL)                       00079802
            MOVE 2 TO WRK-FLAG                                          00079902
           ELSE                                                         00080002
            MOVE 1 TO WRK-FLAG                                          00080102
           END-IF.                                                      00080202
      *-----------------------------------------------------            00080302
       1000-99-FIM.                                  EXIT.              00080402
      *-----------------------------------------------------            00080502
                                                                        00080602
      *-----------------------------------------------------            00080702
       2000-PROCESSAR                             SECTION.              00080802
      *-----------------------------------------------------            00080902
           EVALUATE TRUE                                                00081002
             WHEN EIBAID = ''''                                         00082002
              PERFORM 2050-LIMPAR                                       00083002
             WHEN EIBAID = ':'                                          00084002
              PERFORM 2100-SAIR                                         00085002
             WHEN EIBAID = '5'                                          00086002
              PERFORM 2150-CONSULTAR                                    00087002
            END-EVALUATE.                                               00088002
      *-----------------------------------------------------            00089002
       2000-99-FIM.                                  EXIT.              00090002
      *-----------------------------------------------------            00100002
                                                                        00110002
      *-----------------------------------------------------            00120002
       2050-LIMPAR                                SECTION.              00130002
      *-----------------------------------------------------            00140002
           INITIALIZE MAPA01O.                                          00150002
      *-----------------------------------------------------            00160002
       2050-99-FIM.                                  EXIT.              00170002
      *-----------------------------------------------------            00180002
                                                                        00190002
      *-----------------------------------------------------            00200002
       2100-SAIR                                  SECTION.              00201002
      *-----------------------------------------------------            00201102
            EXEC CICS SEND                                              00201202
             FROM(WRK-SAIDA)                                            00201302
             ERASE                                                      00201402
            END-EXEC.                                                   00201502
                                                                        00201602
            EXEC CICS                                                   00201702
             RETURN                                                     00201802
            END-EXEC.                                                   00201902
      *-----------------------------------------------------            00202002
       2100-99-FIM.                                  EXIT.              00202102
      *-----------------------------------------------------            00202202
                                                                        00202302
      *-----------------------------------------------------            00202402
       2150-CONSULTAR                             SECTION.              00202502
      *-----------------------------------------------------            00202602
             MOVE IDI TO WRK-CHAVE.                                     00202702
              EXEC CICS LINK                                            00202802
                        PROGRAM('F22COVSA')                             00202902
                        COMMAREA(WRK-DADOS)                             00203002
                        RESP(WRK-RESP)                                  00205002
              END-EXEC.                                                 00206002
                                                                        00207002
            EVALUATE WRK-STATUS                                         00207102
              WHEN '1'                                                  00207202
               MOVE WRK-NOME                  TO NOMEO                  00209002
               MOVE WRK-SETOR                 TO SETORO                 00210002
               MOVE WRK-SALARIO               TO SALARIOO               00220002
               MOVE WRK-DATADIA               TO DATADIAO               00230003
               MOVE WRK-DATAMES               TO DATAMESO               00240003
               MOVE WRK-DATAANO               TO DATAANOO               00250003
               MOVE WRK-EMAIL                 TO EMAILO                 00251002
               MOVE 'ENCONTRADO'              TO MSGO                   00252002
              WHEN '2'                                                  00253002
               INITIALIZE MAPA01O                                       00253105
               MOVE 'REGISTRO NAO ENCONTRADO' TO MSGO                   00253202
            END-EVALUATE.                                               00253302
      *-----------------------------------------------------            00254002
       2150-99-FIM.                                  EXIT.              00255002
      *-----------------------------------------------------            00256002
                                                                        00257002
      *-----------------------------------------------------            00258002
       3000-FINALIZAR                             SECTION.              00259002
      *-----------------------------------------------------            00260002
           IF WRK-FLAG EQUAL 2                                          00270002
             EXEC CICS SEND                                             00280002
               MAPSET('F22CIM1')                                        00290002
               MAP('MAPA01')                                            00300002
                ERASE                                                   00310002
               MAPONLY                                                  00320002
             END-EXEC                                                   00330002
           ELSE                                                         00331002
             EXEC CICS SEND                                             00332002
               MAPSET('F22CIM1')                                        00333002
               MAP('MAPA01')                                            00334002
               DATAONLY                                                 00335002
               FROM(MAPA01O)                                            00336002
             END-EXEC                                                   00337002
           END-IF.                                                      00338002
      *-----------------------------------------------------            00339002
       3000-99-FIM.                                  EXIT.              00340002
      *-----------------------------------------------------            00350002
