      *================================================================*00010023
       IDENTIFICATION                            DIVISION.              00020000
      *================================================================*00030023
       PROGRAM-ID. MD022204.                                            00040000
       AUTHOR.     VICTOR LEAL.                                         00041023
                                                                        00042023
      *================================================================*00043023
      *                      T R E I N A M E N T O                     *00044023
      *================================================================*00045023
      *  PROGRAMA...: MD022004.                                        *00046023
      *  PROGRAMADOR: VICTOR LEAL    -    TREINAMENTO                  *00060023
      *  EMPRESA... : FOURSYS                                          *00070023
      *  INSTRUTOR..: IVAN SANCHES   -    INSTRUTOR                    *00071023
      *  DATA.......: 12/07/2022                                       *00072023
      *----------------------------------------------------------------*00072123
      * VERSAO     DATA       AUTOR          DESCRICAO DA MODIFICACAO  *00072223
      *  1.0     12/07/22  VICTOR LEAL                 --              *00072323
      *----------------------------------------------------------------*00072423
      *  OBJETIVO...: FAZER A COMPARACAO ENTRE 2 CHAVES, APOS  UTILIZAR*00080023
      *               O COMANDO REWRITE EM UM ARQUIVO VSAM.            *00080123
      *----------------------------------------------------------------*00080223
      *----------------------------------------------------------------*00080323
      *                     A L T E R A C O E S                        *00080423
4SYS22*----------------------------------------------------------------*00080523
  .   *    PROGRAMADOR...: VICTOR LEAL                                 *00080623
  .   *    DATA..........: 13/07/2022                                  *00080723
  .   *----------------------------------------------------------------*00080823
  .   *    OBJETIVO......: CRIAR ARQUIVO DE SAIDA (EXCSAIDA) PARA      *00080923
  .   *                    GRAVAR EXCESSAO.                            *00081023
4SYS22*----------------------------------------------------------------*00081123
      *  ARQUIVOS:                                                     *00081223
      *  DDNAME             I/O           INCLUDE/BOOK                 *00081323
      *  EVSA0407           I/O            ---------                   *00081423
      *  MOV1207             I             ---------                   *00081523
4SYS22*  EXC1207             O             ---------                   *00081623
      *================================================================*00081723
                                                                        00081800
      *================================================================*00081923
       ENVIRONMENT                               DIVISION.              00082000
      *================================================================*00082123
      *----------------------------------------------------------------*00082223
       CONFIGURATION                             SECTION.               00082300
      *----------------------------------------------------------------*00082423
       SPECIAL-NAMES.                                                   00082500
           DECIMAL-POINT  IS COMMA.                                     00082600
                                                                        00082700
      *----------------------------------------------------------------*00082823
       INPUT-OUTPUT                              SECTION.               00082900
      *----------------------------------------------------------------*00083023
       FILE-CONTROL.                                                    00083100
           SELECT EVSA0407 ASSIGN TO EVSA0407                           00083200
               ORGANIZATION IS INDEXED                                  00083300
               ACCESS MODE  IS DYNAMIC                                  00083400
               RECORD KEY   IS ARQ-CHAVE                                00083500
               FILE STATUS  IS WRK-FS-EVSA0407.                         00083600
                                                                        00084000
           SELECT MOV1207 ASSIGN TO MOV1207                             00085001
               FILE STATUS IS WRK-FS-MOV1207.                           00086001
                                                                        00086117
4SYS22     SELECT EXC1207 ASSIGN TO EXC1207                             00086219
4SYS22         FILE STATUS IS WRK-FS-EXC1207.                           00086319
      *================================================================*00088023
       DATA                                      DIVISION.              00089000
      *================================================================*00090023
      *----------------------------------------------------------------*00100023
       FILE                                      SECTION.               00110000
      *----------------------------------------------------------------*00120023
                                                                        00120123
      *----------------------------------------------------------------*00121023
      *  I/O - DADOS DO ARQUIVO DE ENTRADA E SAIDA(EVSA0704) - LRECL=19*00122023
      *----------------------------------------------------------------*00123023
       FD EVSA0407.                                                     00130000
       01 REG-EVSA0407.                                                 00130100
          05 ARQ-CHAVE.                                                 00130200
             10 ARQ-AGENCIA        PIC X(04).                           00130301
             10 ARQ-CONTA          PIC X(05).                           00130401
          05 ARQ-SALARIO           PIC X(10).                           00130501
                                                                        00130623
      *----------------------------------------------------------------*00130723
      *  INPUT - DADOS DO ARQUIVO DE ENTRADA (MOV1207) - LRECL=19      *00130823
      *----------------------------------------------------------------*00130923
       FD MOV1207                                                       00131001
            RECORDING MODE IS F                                         00131100
            BLOCK CONTAINS 0 RECORDS.                                   00131200
       01 FD-MOV1207.                                                   00131302
          05 FD-MOV1207-CHAVE.                                          00132003
             10 FD-MOV1207-AGENCIA PIC X(04).                           00132101
             10 FD-MOV1207-CONTA   PIC X(05).                           00132201
          05 FD-MOV1207-SALARIO    PIC X(10).                           00132303
                                                                        00132423
4SYS22*----------------------------------------------------------------*00132523
  .   *  OUTPUT - DADOS DO ARQUIVO DE SAIDA (EXC1207) - LRECL=19       *00132623
  .   *----------------------------------------------------------------*00132723
  .    FD EXC1207                                                       00132923
  .         RECORDING MODE IS F                                         00133019
  .         BLOCK CONTAINS 0 RECORDS.                                   00133119
4SYS22 01 FD-EXC1207               PIC X(19).                           00133219
      *----------------------------------------------------------------*00133323
       WORKING-STORAGE                           SECTION.               00134000
      *----------------------------------------------------------------*00134123
       01 WRK-EVSA0407.                                                 00134200
          05 WRK-CHAVE.                                                 00134300
             10 WRK-AGENCIA     PIC X(04).                              00134400
             10 WRK-CONTA       PIC X(05).                              00134500
          05 WRK-SALARIO        PIC X(10).                              00134600
      *----------------------------------------------------------------*00134823
       01 FILLER PIC X(48) VALUE                                        00134900
           '--------------AREA FILE STATUS---------------'.             00135000
      *----------------------------------------------------------------*00136023
       77 WRK-FS-EVSA0407    PIC 9(02).                                 00136100
       77 WRK-FS-MOV1207     PIC 9(02).                                 00136201
4SYS22 77 WRK-FS-EXC1207     PIC 9(02).                                 00136319
      *----------------------------------------------------------------*00136423
       01 FILLER PIC X(48) VALUE                                        00136515
           '--------------AREA PARA ACUMULAR-------------'.             00136615
      *----------------------------------------------------------------*00136723
       77 WRK-REGLIDOS       PIC 9(02) VALUE ZEROS.                     00136815
       77 WRK-REGGRAV        PIC 9(02) VALUE ZEROS.                     00136915
       77 WRK-REGEXCE        PIC 9(02) VALUE ZEROS.                     00137015
      *================================================================*00138223
       PROCEDURE                                 DIVISION.              00138401
      *================================================================*00138523
                                                                        00138623
      ******************************************************************00138723
      *                R O T I N A   P R I N C I P A L                 *00138823
      ******************************************************************00138923
                                                                        00139023
      *----------------------------------------------------------------*00139123
       0000-PRINCIPAL                             SECTION.              00139200
      *----------------------------------------------------------------*00139323
           PERFORM 1000-INICIALIZAR.                                    00139400
             PERFORM 1050-TESTAVAZIO.                                   00139514
           PERFORM 2000-PROCESSAR.                                      00139608
           PERFORM 3000-FINALIZAR.                                      00139700
           STOP RUN.                                                    00139800
      *----------------------------------------------------------------*00139923
       0000-99-FIM.                                  EXIT.              00140000
      *----------------------------------------------------------------*00140123
                                                                        00140223
      ******************************************************************00140323
      *                   I N I C I A L I Z A R                        *00140423
      ******************************************************************00140523
                                                                        00140700
      *----------------------------------------------------------------*00140823
       1000-INICIALIZAR                           SECTION.              00140900
      *----------------------------------------------------------------*00141023
           OPEN I-O EVSA0407.                                           00141106
            PERFORM 1100-TESTA-EVSA0407.                                00141200
                                                                        00141300
           OPEN INPUT MOV1207.                                          00141401
            PERFORM 1200-TESTA-MOV1207.                                 00141501
                                                                        00141618
4SYS22     OPEN OUTPUT EXC1207.                                         00141719
4SYS22      PERFORM 1300-TESTA-EXC1207.                                 00141819
      *----------------------------------------------------------------*00141923
       1000-99-FIM.                                  EXIT.              00142000
      *----------------------------------------------------------------*00142123
                                                                        00142214
      *----------------------------------------------------------------*00142323
       1050-TESTAVAZIO                            SECTION.              00142414
      *----------------------------------------------------------------*00142523
           READ MOV1207                                                 00142614
            IF WRK-FS-MOV1207 NOT EQUAL ZEROS                           00142714
             DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                    00142814
             GOBACK                                                     00142914
            END-IF.                                                     00143014
      *----------------------------------------------------------------*00143123
       1050-99-FIM.                                  EXIT.              00143214
      *----------------------------------------------------------------*00143323
                                                                        00143400
      *----------------------------------------------------------------*00143523
       1100-TESTA-EVSA0407                        SECTION.              00143600
      *----------------------------------------------------------------*00143723
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00143800
            DISPLAY 'ERRO NA ABERTURA DO ARQUIVO EVSA0407'              00143910
            DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                     00144000
            GOBACK                                                      00144109
           END-IF.                                                      00144200
      *----------------------------------------------------------------*00144323
       1100-99-FIM.                                  EXIT.              00144400
      *----------------------------------------------------------------*00144523
                                                                        00144600
      *----------------------------------------------------------------*00144723
       1200-TESTA-MOV1207                         SECTION.              00144801
      *----------------------------------------------------------------*00144923
           IF WRK-FS-MOV1207 NOT EQUAL ZEROS                            00145001
            DISPLAY 'ERRO NA ABERTURA DO ARQUIVO MOV1207'               00145101
            DISPLAY 'CODIGO VSAM: ' WRK-FS-MOV1207                      00145201
            GOBACK                                                      00145309
           END-IF.                                                      00145400
      *----------------------------------------------------------------*00145523
       1200-99-FIM.                                  EXIT.              00145600
      *----------------------------------------------------------------*00145723
                                                                        00145818
4SYS22*----------------------------------------------------------------*00145923
  .    1300-TESTA-EXC1207                         SECTION.              00146019
  .   *----------------------------------------------------------------*00146123
  .        IF WRK-FS-EXC1207 NOT EQUAL ZEROS                            00146219
  .         DISPLAY 'ERRO NA ABERTURA DO ARQUIVO EXC1207'               00146319
  .         DISPLAY 'CODIGO VSAM: ' WRK-FS-EXC1207                      00146419
  .         GOBACK                                                      00146519
  .        END-IF.                                                      00146619
  .   *----------------------------------------------------------------*00146723
  .    1300-99-FIM.                                  EXIT.              00146819
4SYS22*----------------------------------------------------------------*00146923
                                                                        00147018
      ******************************************************************00147123
      *                   P R O C E S S A R                            *00147223
      ******************************************************************00147323
                                                                        00147423
      *----------------------------------------------------------------*00147523
       2000-PROCESSAR                             SECTION.              00147600
      *----------------------------------------------------------------*00147723
           PERFORM UNTIL WRK-FS-MOV1207 EQUAL 10                        00147807
             ADD 1 TO WRK-REGLIDOS                                      00147915
             MOVE FD-MOV1207-CHAVE TO ARQ-CHAVE                         00148009
             READ EVSA0407                                              00148114
             IF WRK-FS-EVSA0407 EQUAL ZEROS                             00148201
               MOVE FD-MOV1207 TO WRK-EVSA0407                          00148301
               REWRITE REG-EVSA0407 FROM WRK-EVSA0407                   00148401
               ADD 1 TO WRK-REGGRAV                                     00148515
                IF WRK-FS-EVSA0407 EQUAL ZEROS                          00148601
                 DISPLAY 'REGISTRO ATUALIZADO: ' WRK-EVSA0407           00148701
                END-IF                                                  00148801
             ELSE                                                       00148901
              MOVE FD-MOV1207 TO WRK-EVSA0407                           00149021
              ADD 1 TO WRK-REGEXCE                                      00149115
4SYS22        WRITE FD-EXC1207 FROM WRK-EVSA0407                        00149219
             END-IF                                                     00149307
            READ MOV1207                                                00149409
           END-PERFORM.                                                 00149507
      *----------------------------------------------------------------*00149623
       2000-99-FIM.                                  EXIT.              00149700
      *----------------------------------------------------------------*00149823
                                                                        00149923
      ******************************************************************00150023
      *                   F I N A L I Z A R                            *00150123
      ******************************************************************00150223
                                                                        00150300
      *----------------------------------------------------------------*00150923
       3000-FINALIZAR                             SECTION.              00151000
      *----------------------------------------------------------------*00151123
           CLOSE EVSA0407.                                              00151200
            PERFORM 3100-TESTA-EVSA0407.                                00151300
           CLOSE MOV1207.                                               00151401
            PERFORM 3200-TESTA-MOV1207.                                 00151501
4SYS22     CLOSE EXC1207.                                               00151719
4SYS22      PERFORM 3300-TESTA-EXC1207.                                 00151819
                                                                        00151900
           DISPLAY '  '.                                                00152000
           DISPLAY 'REGISTROS LIDOS: '      WRK-REGLIDOS.               00152115
           DISPLAY 'REGISTROS REGRAVADOS: ' WRK-REGGRAV.                00152215
           DISPLAY 'CHAVE NAO ENCONTRADA: ' WRK-REGEXCE.                00152315
           DISPLAY 'FIM DE PROCESSAMENTO'.                              00153000
      *----------------------------------------------------------------*00154023
       3000-99-FIM.                                  EXIT.              00155000
      *----------------------------------------------------------------*00156023
                                                                        00157000
      *----------------------------------------------------------------*00158023
       3100-TESTA-EVSA0407                        SECTION.              00159000
      *----------------------------------------------------------------*00160023
           IF WRK-FS-EVSA0407 NOT EQUAL ZEROS                           00161000
            DISPLAY 'ERRO NO FECHAMENTO DO ARQUIVO EVSA0407'            00161100
            DISPLAY 'CODIGO VSAM: ' WRK-FS-EVSA0407                     00161200
            GOBACK                                                      00161310
           END-IF.                                                      00161400
      *----------------------------------------------------------------*00161623
       3100-99-FIM.                                  EXIT.              00161700
      *----------------------------------------------------------------*00161823
                                                                        00161900
      *----------------------------------------------------------------*00162023
       3200-TESTA-MOV1207                        SECTION.               00162101
      *----------------------------------------------------------------*00162223
           IF WRK-FS-MOV1207 NOT EQUAL ZEROS                            00162301
            DISPLAY 'ERRO NO FECHAMENTO DO ARQUIVO MOV1207'             00162401
            DISPLAY 'CODIGO VSAM: ' WRK-FS-MOV1207                      00162501
            GOBACK                                                      00162610
           END-IF.                                                      00162700
      *----------------------------------------------------------------*00162923
       3200-99-FIM.                                  EXIT.              00163000
      *----------------------------------------------------------------*00164023
                                                                        00165018
4SYS22*----------------------------------------------------------------*00166023
  .    3300-TESTA-EXC1207                        SECTION.               00167019
  .   *----------------------------------------------------------------*00168023
  .        IF WRK-FS-EXC1207 NOT EQUAL ZEROS                            00169019
  .         DISPLAY 'ERRO NO FECHAMENTO DO ARQUIVO EXC1207'             00170019
  .         DISPLAY 'CODIGO VSAM: ' WRK-FS-EXC1207                      00180019
  .         GOBACK                                                      00190019
  .        END-IF.                                                      00200019
  .   *----------------------------------------------------------------*00210023
  .    3300-99-FIM.                                  EXIT.              00220019
4SYS22*----------------------------------------------------------------*00230023
